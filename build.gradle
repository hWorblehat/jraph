plugins {
	id 'java-library'
	id 'groovy'
	id 'jacoco'
	id "org.sonarqube" version "2.6"

	id 'org.ajoberstar.grgit' version "2.3.0"
	id 'org.uulib.reckon' version "0.1.0-alpha.2"
	id 'org.uulib.gittag' version "0.2.0-alpha.2"
}

import java.time.Instant
ext.buildTime = Instant.now()

group = 'org.hWorblehat'
description = 'Graph algorithms in Java'
ext.basePackage = 'org.hworblehat.jraph'

sourceCompatibility = 1.8
targetCompatibility = 1.8

apply from: 'dependencies.gradle'

jacoco {
	toolVersion = '0.8.3'
}

if(System.env.CI) {

	jacocoTestReport {
		reports {
			xml.enabled true
			html.enabled false
		}
	}
}

reckon {
	vcs = git(project.grgit)

	def stage = projectProperty 'stage' orElse 'development'
	def scope = projectProperty 'scope' orElse 'patch'

	normalVersion = basedOn(stage) {
		normally scopedUsing(scope)
		when 'development' then '0.0.0'
	}

	preReleaseVersion = basedOn(stage) {
		normally stageUsing(stage)
		when 'final' then none
		when 'development' then compound {
			preRelease = timestamp(buildTime)
			buildMetadata = commitId
		}
	}
}

gradletag {
	tags {
		versionTag {
			tag = "v${project.version}"
		}
	}
}

apply from: 'gradle/package.gradle'
apply from: 'gradle/publish.gradle'
